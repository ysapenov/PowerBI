Net Sales Units by Category = 
// this is the measure, which dynamically calculate the cover category 
// in specific Channels and stores per each Link Line depends on selected slicers.
// Cover categories are predefined in the custom table.
Net Sales Units by Category = 
VAR _table =
ADDCOLUMNS(
    'ProductLinkLine',
    "Units",
    CALCULATE(
        SUM(Trading[Adj Net Sales Units])
    ),
    "Cover",
    VAR _sales =
    CALCULATE(
        SUM(Trading[Adj Net Sales Units]),
        Channel[Channel Id] IN {10,20},
        Location[Location Type] = "Store"
    )
    VAR _stock = 
    CALCULATE(
        SUM(Stock[Stock Quantity]),
        Channel[Channel Id] IN {10,20},
        Location[Location Type] = "Store"
    )
    VAR _cover =
    DIVIDE(
        _stock,
        _sales
    )
    RETURN
    _cover
)
RETURN
SWITCH(
    SELECTEDVALUE('Cover Category'[Id]),
    1, SUMX(FILTER(_table,[Cover] < 0),[Units]),
    2, SUMX(FILTER(_table,[Cover] >= 0 && [Cover] < 6),[Units]),
    3, SUMX(FILTER(_table,[Cover] >= 6 && [Cover] < 10),[Units]),
    4, SUMX(FILTER(_table,[Cover] >= 10 && [Cover] < 15),[Units]),
    5, SUMX(FILTER(_table,[Cover] >= 15 && [Cover] < 20),[Units]),
    6, SUMX(FILTER(_table,[Cover] >= 20),[Units]),
    SUMX(_table,[Units])
)